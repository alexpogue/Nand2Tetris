// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=curVal, out=incremented);

    /* if inc==1, increment curVal before passing along */
    Mux16(a=curVal, b=incremented, sel=inc, out=incrementedOrCurVal);

    /* if load==1, don't pay attention to prevous output, and just use 'in' port */
    Mux16(a=incrementedOrCurVal, b=in, sel=load, out=loadedIncrementedOrCurVal);

    /* if reset==1, don't pay attention to previous output, and just use all zeros */
    Mux16(a=loadedIncrementedOrCurVal, b=false, sel=reset, out=regInput);
    
    Register(in=regInput, load=true, out=curVal, out=out);
}
