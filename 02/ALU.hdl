// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    /* decide whether to zero out x and y */
    Mux16(a=x, b=false, sel=zx, out=zerosOrX);
    Mux16(a=y, b=false, sel=zy, out=zerosOrY);

    /* "not" the inputs */
    Not16(in=zerosOrX, out=notX);
    Not16(in=zerosOrY, out=notY);

    /* decide whether to use x or not'd version */
    Mux16(a=zerosOrX, b=notX, sel=nx, out=finalx);
    Mux16(a=zerosOrY, b=notY, sel=ny, out=finaly);

    /* compute added/anded x and y values, decide which to use according to f */
    Add16(a=finalx, b=finaly, out=aplusb);
    And16(a=finalx, b=finaly, out=aandb);
    Mux16(a=aandb, b=aplusb, sel=f, out=result);

    /* decide whether to negate result, out[15] is negative bit */
    Not16(in=result, out=notresult);
    Mux16(a=result, b=notresult, sel=no, out[15]=ng, out[0..7]=lsbHalf, out[8..15]=msbHalf, out=out);

    /* if there's a 1 in any spot, then out != 0, negate that to get out == 0 */
    Or8Way(in=lsbHalf, out=lsbHalfHas1);
    Or8Way(in=msbHalf, out=msbHalfHas1);
    Or(a=lsbHalfHas1, b=msbHalfHas1, out=notEqualZero);
    Not(in=notEqualZero, out=zr);
}
